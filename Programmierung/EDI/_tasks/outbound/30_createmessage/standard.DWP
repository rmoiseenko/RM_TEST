//##############################################################################
//##############################################################################
// Beschreibung:
//   Dieses Programm erstellt Nechrichten zu allen Beleglisten
//
// Rückgabe: array - EDITaskResult
//   {
//      {'EDITaskResult.totalProcessed', <integer>},
//      {'EDITaskResult.succeededCount', <integer>},
//      {'EDITaskResult.failedCount',    <integer>}
//   }
//##############################################################################
//##############################################################################

|
   m_oErrorCallback:P1,
   m_oProgressCallback:P2,

   m_oDM,
   m_aTaskResult
|

//##############################################################################
//##############################################################################
// Funktionen
//##############################################################################
//##############################################################################

//******************************************************************************
// Diese Funktion erstellt Nachrichten zu Beleglisten
//
// @result: array - EDITaskResult
//******************************************************************************
function CreateMessageForDocumentLists(oDM:O, oErrorCallback:O, oProgressCallback:O)
| oEDIDOCL, aProcessResult, nSucceededCount, nFailedCount, cEDIPROFScript |

   nSucceededCount := 0,
   nFailedCount    := 0,

   oEDIDOCL := MyGetTable(oDM, waEDIDOCL, 'EDIDOCL_TASK'),
   oEDIDOCL.CbIndexName := 'ID',

   oEDIDOCL.CbFilter := "PROC_STEP=" + str(gl_EDI_ProcessStep_EDIDOCL_InhouseFormatCreated),
   oEDIDOCL.Filtered := True,
   startseq
      oEDIDOCL.CbFirst(),
      while not oEDIDOCL.CbEof do
         startseq
            cEDIPROFScript := LookUp(waEDIPROF, 'ID', {{oEDIDOCL:EDIPROF_ID}}, 'SCRIPT_MSG'),
            if Empty(cEDIPROFScript) then
               SetError('No selected script for creation of messages.'),
            else
               aProcessResult := Call(cEDIPROFScript, oDM, oEDIDOCL:ID),
            endif,
            if EDI_ProcessResult_IsSuccess(aProcessResult) then
               nSucceededCount++,
            else
               nFailedCount++,
               EDI_Process_UpdateError(oErrorCallback, 'DocumentList-ID ' + str(oEDIDOCL:ID) + ': ' + EDI_ProcessResult_GetErrorMessage(aProcessResult)),
            endif,
         onerror
            EDI_Process_UpdateError(oErrorCallback, GetErrorObj(), GetErrorCode(), GetErrorStack(true)),
         stopseq,

         oEDIDOCL.CbNext(),
      end,
   always
      oEDIDOCL.Filtered := False,
   stopseq,

   result := {},
   KVU_Set(result, 'EDITaskResult.totalProcessed', nSucceededCount + nFailedCount),
   KVU_Set(result, 'EDITaskResult.succeededCount', nSucceededCount),
   KVU_Set(result, 'EDITaskResult.failedCount',    nFailedCount),
end,

//##############################################################################
//##############################################################################
// Hauptprogramm
//##############################################################################
//##############################################################################

m_oDM := CreateObject('TBeDbGet'),
startseq
   m_aTaskResult := CreateMessageForDocumentLists(m_oDM, m_oErrorCallback, m_oProgressCallback),
always
   DestroyObject(m_oDM),
stopseq,

m_aTaskResult
