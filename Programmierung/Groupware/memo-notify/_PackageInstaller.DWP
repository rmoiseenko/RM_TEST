//##############################################################################
//##############################################################################
// Beschreibung:
//   Installations-Script für Package "memo-notify"
//##############################################################################
//##############################################################################

|
   // Workareas
   nWorkarea_KDNGMN,

   aDialogResult,
   cUsername,
   cPassword,

   i,
   oDM
|

//##############################################################################
// Konstanten
//##############################################################################

nWorkarea_KDNGMN := 710,

//##############################################################################
// Funktionen
//##############################################################################

//******************************************************************************
// Diese Funktion fügt alle Felddefinitionen für die Tabelle "KDNGMN" hinzu.
// @result: null - keine Rückgabe
//******************************************************************************
function KDNGMN_AddFields(oTableDefinition:R)
   PCKGU_TableDefinition_AddField(oTableDefinition, 'USER_ID',     3, 0, ftString,   'Benutzer-Nr.',                   '', 0, 0, '','', 0,                   true, false, false, true, false, true, '', false, 0, 0, '', '', '', '', '', ''),
   PCKGU_TableDefinition_AddField(oTableDefinition, 'MEMO_ID',     0, 0, ftInteger,  'Memo-ID',                        '', 0, 0, '','', 0,                   true, false, false, true, false, true, '', false, 0, 0, '', '', '', '', '', ''),
   PCKGU_TableDefinition_AddField(oTableDefinition, 'TYP',         1, 0, ftString,   'Typ des Notify-Eintrags',        'Unterscheidung zwischen "N"euen und "G"eänderten Memos',
                                                                                                                           0, 0, '','', 0,                   true, false, false, true, false, true, '', false, 0, 0, '', '', '', '', '', ''),
end,

//******************************************************************************
// Diese Funktion fügt alle Indexes für die Tabelle "KDNGMN" hinzu.
// @result: null - keine Rückgabe
//******************************************************************************
function KDNGMN_AddIndexes(oTableDefinition:R)
   PCKGU_TableDefinition_AddIndex(oTableDefinition,    'USER_ID_MEMO_ID', {'USER_ID', 'MEMO_ID'}, '', '', false, false, false, 512),
end,

//******************************************************************************
// Die Funktion erzeugt eine vollständige Tabellendefinition der Tabelle KDNGMN.
// @result: array - TableDefinition der Tabelle KDNGMN
//******************************************************************************
function KDNGMN_CreateTableDefinition()
   result := PCKGU_TableDefinition_Create('KDNGMN', nWorkarea_KDNGMN, 'Groupware Memo-Notify', '', True, False),
   KDNGMN_AddFields(result),
   KDNGMN_AddIndexes(result),
end,

//##############################################################################
//##############################################################################
// Installer
//##############################################################################
//##############################################################################

startseq
   aDialogResult := DU_InputDialog(
      'ADS-User Anmeldung',
      'Datenbank Anmeldeinformation für einen administrativen User angeben.',
      {
         DU_InputDialog_CreateInputControlDefinition('USERNAME', 'Username', gl_DU_InputControlType_String,   '', 0, 'ADSSYS', true),
         DU_InputDialog_CreateInputControlDefinition('PASSWORD', 'Passwort', gl_DU_InputControlType_Password, '', 0, '',       true)
      },
      {
         DU_InputDialog_CreateButtonDefinition('OK', 'OK', mrOK, 7, true),
         mrCancel
      },
      {}
   ),
   if DU_InputDialogResult_GetModalResult(aDialogResult) = mrOK then
      cUsername := DU_InputDialogResult_GetInputValue(aDialogResult, 'USERNAME'),
      cPassword := DU_InputDialogResult_GetInputValue(aDialogResult, 'PASSWORD'),

      oDM := CreateObject('TBeDbGet'),
      startseq
         oDM.CbTransaction_Begin(),
         startseq
            // HINWEIS: Um eine bestehende Installation der Tabellen zu löschen
            //          müssen die folgenden folgenden Zeilen einkommentiert werden:
            // PCKGU_RemoveTableAndMetaInfo(oDM, nWorkarea_KDNGMN, 'KDNGMN', cUsername, cPassword),

            PCKGU_AddTableAndMetaInfo(oDM, KDNGMN_CreateTableDefinition(), cUsername, cPassword),

            PCKGU_DeleteAllRightsBerCacheFiles(),

            oDM.CbTransaction_Commit(),
         always
            oDM.CbTransaction_TryRollback(),
         stopseq,
      always
         DestroyObject(oDM),
      stopseq,
   endif,
onerror
   GU_ShowStdErrorMessage('Fehler im Programm ' + ProgName(), GetErrorObj()),
stopseq,

